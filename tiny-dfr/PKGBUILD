# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgname=tiny-dfr
pkgver=0.3.4
pkgrel=2
pkgdesc='Apple silicon touch bar input / display daemon'
arch=('aarch64')
url=https://github.com/WhatAmISupposedToPutHere/tiny-dfr
license=('MIT' 'APACHE')
makedepends=('rustup')
depends=('pango' 'libinput' 'gdk-pixbuf2' 'librsvg')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz" "0001-Backlight-Support-upstreamed-mipi-panel-driver.patch")
sha512sums=('ad40de68d073205a4be65ffbec4e97aea6b0c52b8c6d4b47258ef28a377dc7933d23aca60f02f4ef070b880f6cad0fe2d783628b2a6a872dbc80d46548cd7732'
            'aa9a5eb27d725ba4280075e1057421362496fbd346efedc9dfdcc6b9563469d67db0b86f6e82af0f52a3fe5b46d46f23b17090c096fa8ecf963a2a44b5849ee9')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
  patch -Np1 < "${srcdir}/0001-Backlight-Support-upstreamed-mipi-panel-driver.patch"
}

build() {
  cd $pkgname-$pkgver
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 share/$pkgname/* -t "$pkgdir/usr/share/$pkgname"
  install -Dm644 etc/systemd/system/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 etc/udev/rules.d/99-touchbar-seat.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-seat.rules"
  install -Dm644 etc/udev/rules.d/99-touchbar-tiny-dfr.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-tiny-dfr.rules"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENSE.material "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.material
}

# vim:set ts=2 sw=2 et:
