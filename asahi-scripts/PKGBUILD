# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=asahi-scripts
pkgver=20240411
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('3350cabf9716fab2a18422800210b8a16de11317e3bbe8ec99c1242466f9afc1')
b2sums=('a625556f8bf1636c187705012ade561e83edc5ab358cd22c8d5b8b9f36cf8c1c1abf4087978c87e44eafb625b8513eeefb846155b7e3d68f5397dc3580bd971a')
backup=(etc/m1n1.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch

  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
