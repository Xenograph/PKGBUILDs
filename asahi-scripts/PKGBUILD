# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgname=asahi-scripts
pkgver=20250130
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asahi-alarm/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('3a53115d6c79dbc77949a2744cbb3bc08dbeead7ae1c0c6c3f6e30a584e8c0c5')
b2sums=('fc51d44ccd8e01985f01c3d33be9888ea95eeff1200ad1f32c2cb416d47fbaa2cf4a65348af7227644dd4bada71bc697c3a1f441160248f55b668972494f5634')
backup=(etc/m1n1.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch
  # re-add DTBS variable
  sed -i -e '/${CONFIG/i : ${DTBS:=$(/bin/ls -d /lib/modules/*-ARCH | sort -rV | head -1)/dtbs/*.dtb}' "$pkgdir/usr/bin/update-m1n1"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
