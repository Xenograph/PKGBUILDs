# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgbase=FEX-Emu
pkgname=FEX-Emu
pkgver=2502
pkgrel=1
pkgdesc="Fast x86 emulation frontend"
arch=(aarch64)
url="https://github.com/FEX-Emu/FEX"
license=(MIT)
makedepends=(
  cmake
  ninja
  git
  lld
  llvm
  clang
  libepoxy
  python
  qt6-declarative
)
depends=(
  passt
  squashfuse
  squashfs-tools
  erofs-utils
)
source=("git+https://github.com/FEX-Emu/FEX.git#tag=FEX-${pkgver}")
sha256sums=('SKIP')
install=FEX-Emu.install

prepare() {
  cd "${srcdir}/FEX"
  # Needed because the repo has submodules
  git submodule update --init --recursive
}

build() {
  cd ${srcdir}/FEX
  rm -rf build
  mkdir -p build || true
  cd build
  CC=clang CXX=clang++ cmake \
    -G Ninja \
    -DENABLE_OFFLINE_TELEMETRY=OFF \
    -DUSE_FEXCONFIG_TOOLKIT=qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_LINKER=lld \
    -DENABLE_LTO=ON \
    -DBUILD_TESTS=OFF \
    -DBUILD_FEX_LINUX_TESTS=OFF \
    -DENABLE_ASSERTIONS=ON \
    ..
  ninja
}

package() {
  cd ${srcdir}/FEX/build
  DESTDIR="${pkgdir}" ninja install
  mkdir -p "${pkgdir}"/usr/share/fex-emu/RootFS
  mkdir -p "${pkgdir}"/usr/share/fex-emu/overlays
}
